#!/usr/bin/env perl
BEGIN {$^W=1}

## (C) 2020-2021 Mindaugas Margelevicius, Institute of Biotechnology, Vilnius University
## Wrapper for conducting COTHER search given input sequences or MSAs: from sequences
## to profile-structure alignments

use strict;
use FindBin;
use lib "$FindBin::Bin";
use readcfg;
use readopt;
use cosearch;
use File::Spec;
use File::Basename;
use Getopt::Long;
use POSIX qw(strftime);

my  $MYPROGNAME = basename($0);
my  $usage = <<EOIN;

Wrapper for conducting COTHER search given input sequence(s) or MSA(s). It
connects all logic from sequences to threading and profile-structure alignments.
(C)2020-2021 Mindaugas Margelevicius, Institute of Biotechnology, Vilnius University

Usage:
$0 <Options>

Options:

--in <input>     Input sequence(s) (plain or in FASTA) or MSAs in FASTA or 
                 STOCKHOLM format, used as the queries to search database(s).

--opt <options>  Filename of options for this job.

--status <file>  Name of the output status file of computation progress
                 messages.

--comerlog <logfile> Name of the output log file generated by the COTHER
                 during a search.

--reslst <file>  Name of the output file listing the results files for the
                 queries.

--results <file> Name of the output compressed file containing the results
                 files for all queries. (Extension .gz will be added.)

--err <errfile>  Name of the file of high-level error messages to write to on
                 error.

--nopro          Do not construct COTHER profile (implies --norun).
--norun          Do not run COTHER.

--help           This help text.

EOIN


my  $INPFILENAME = '';
my  $OPTFILENAME = '';
my  $STAFILENAME = '';
my  $COMLOGFILENAME = '';
my  $RESLSTFILENAME = '';
my  $RESULTSFILENAME = '';
my  $ERRFILENAME = '';
my  $NOPRO = 0;
my  $NORUN = 0;
my  $Fail = 0;

my  $result = GetOptions(
               'in=s'       => \$INPFILENAME,
               'opt=s'      => \$OPTFILENAME,
               'status=s'   => \$STAFILENAME,
               'comerlog=s' => \$COMLOGFILENAME,
               'reslst=s'   => \$RESLSTFILENAME,
               'results=s'  => \$RESULTSFILENAME,
               'err=s'      => \$ERRFILENAME,
               'nopro'      => sub {$NOPRO=1;$NORUN=1;},
               'norun'      => \$NORUN,
               'help|h'     => sub {print $usage; exit(0);}
);

my $cos = cosearch->new();

unless($result) {
    Error("ERROR: $MYPROGNAME: Error in command-line arguments.\n",
            "Command-line arguments error.\n");## h-l error message
    $cos->MyExit(1);
}

$cos->SetOptions(
    $INPFILENAME, $OPTFILENAME, $STAFILENAME, 
    $COMLOGFILENAME, $RESLSTFILENAME, $RESULTSFILENAME, 
    $ERRFILENAME, $NOPRO, $NORUN, 'cother');

$cos->Initialize();

## =============================================================================
## MAIN
##

my  $infmsg = '';##info message
my  $errmsg = "ERROR: $MYPROGNAME: Invalid job options.\n";
my  $hlerrmsg = "Invalid job options.\n";

unless($cos->VerifyOptionValues(\$errmsg, \$hlerrmsg)) {
    $cos->Error($errmsg, $hlerrmsg);## err msg and h-l error message
    $cos->MyExit(1);
}

my  $nqueries = 0;## :shared = 0;##number of individual queries/inputs
my  %inputs;## :shared;##all inputs divided

$errmsg = "ERROR: $MYPROGNAME: Distribution of the input to subdirs failed.\n";
$hlerrmsg = "Parsing or distribution of the input failed.\n";

$cos->ProgressMsg("Preparing input...\n");

unless($cos->DistrInputToSubdirs(
        \$nqueries, \%inputs,
        \$infmsg, \$errmsg, \$hlerrmsg)) {
    $cos->Error($errmsg, $hlerrmsg);## err msg and h-l error message
    $cos->MyExit(1);
}

$cos->ProgressMsg("$infmsg\n") if $infmsg;

$errmsg = '';
$hlerrmsg = '';
my $oneworker;

if($cos->{optionvalues}->{$cos->{optionkeys}->{hhsuite_in_use}}) {
    $cos->ProgressMsg("Searching sequences for MSAs...\n");

    unless($cos->RunThreadsAndWait(
            \&cosearch::ProcessQuery_hhsuite_t,
            $oneworker=1, 
            $nqueries, \%inputs,
            \$errmsg, \$hlerrmsg)) {
        $cos->Error($errmsg);## err msg and h-l error message
        ##continue on...
    }
}

$errmsg = '';
$hlerrmsg = '';

$cos->ProgressMsg("Buiding MSAs by sequence search and constructing profiles...\n");

unless($cos->RunThreadsAndWait(
        \&cosearch::ProcessQuery_t,
        $oneworker=0, 
        $nqueries, \%inputs,
        \$errmsg, \$hlerrmsg)) {
    $cos->Error($errmsg);## err msg and h-l error message
    ##continue on...
}

##print errors and messages if any, returned from threads
$cos->CheckForErrorsandWarnings($nqueries, \%inputs);

unless($cos->{NORUN}) {

    $cos->ProgressMsg("Predicting inter-residue distances and constructing COTHER profiles...\n");

    unless($cos->PredictDistancesAndMakeCOTHERProfiles($nqueries, \%inputs, \$errmsg, \$hlerrmsg)) { 
        $cos->Error($errmsg, $hlerrmsg);
        $cos->MyExit(1);
    }

    $cos->ProgressMsg("Searching by threading using COTHER...\n");

    unless($cos->RunCOTHER($nqueries, \%inputs, \$errmsg, \$hlerrmsg)) { 
        $cos->Error($errmsg, $hlerrmsg);
        $cos->MyExit(1);
    }

    $cos->ProgressMsg("Finalizing results...\n");

    my  @resfilelist;

    unless($cos->MakeResultsList(\@resfilelist, $nqueries, \%inputs, \$errmsg, \$hlerrmsg)) {
        $cos->Error($errmsg, $hlerrmsg);
        $cos->MyExit(1);
    }

    unless($cos->CompressResults(\@resfilelist, \$errmsg, \$hlerrmsg)) {
        $cos->Error($errmsg, $hlerrmsg);
        $cos->MyExit(1);
    }
}

$cos->ProgressMsg("Finished.\n");

$cos->MyExit(0);

## =============================================================================
